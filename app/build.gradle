/*
 * Copyright (c) 2013 â€“ 2015 Ricki Hirner (bitfire web engineering).
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the GNU Public License v3.0
 * which accompanies this distribution, and is available at
 * http://www.gnu.org/licenses/gpl.html
 */

apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'

    defaultConfig {
        applicationId "at.bitfire.davdroid"
        minSdkVersion 14
        targetSdkVersion 22
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    lintOptions {
    	abortOnError false
        disable 'ExtraTranslation'
    }

    packagingOptions {
        exclude 'LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }


    if (project.hasProperty('android.keystore.file')) {
        signingConfigs {
            release {
                storeFile file(project.property('android.keystore.file'))
                storePassword project.property('android.keystore.password')
                keyAlias project.property('android.keystore.alias')
                keyPassword project.property('android.keystore.password')
            }
        }
        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
        }
    }
}

configurations.all {
    exclude module: 'commons-logging'       // undocumented part of Android
}

dependencies {

    compile project(':libs:MemorizingTrustManager')

    // Apache Commons
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'commons-io:commons-io:2.4'
    // Lombok for useful @helpers
    provided 'org.projectlombok:lombok:1.16.4'
    // ical4j for parsing/generating iCalendars
    compile('org.mnode.ical4j:ical4j:2.0-alpha1') {
        // we don't need content builders, see https://github.com/ical4j/ical4j/wiki/Groovy
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    compile('org.slf4j:slf4j-android:1.7.12')       // slf4j is used by ical4j
    // ez-vcard for parsing/generating VCards
    compile('com.googlecode.ez-vcard:ez-vcard:0.9.6') {
        // hCard functionality not needed
        exclude group: 'org.jsoup', module: 'jsoup'
        exclude group: 'org.freemarker', module: 'freemarker'
        // jCard functionality not needed
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
    }
    // dnsjava for querying SRV/TXT records
    compile 'dnsjava:dnsjava:2.1.7'
    // HttpClient 4.3, Android flavour for WebDAV operations
    //compile 'org.apache.httpcomponents:httpclient-android:4.3.5.1'
    compile project(':lib:httpclient-android')
    // SimpleXML for parsing and generating WebDAV messages
    compile('org.simpleframework:simple-xml:2.7.1') {
        exclude group: 'stax', module: 'stax-api'
        exclude group: 'xpp3', module: 'xpp3'
    }
}
